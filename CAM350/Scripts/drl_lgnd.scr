'
'  $Workfile:   drl_lgnd.scr  $
'  $Revision:   1.4  $
'   $Modtime:   27 Mar 2000 09:40:58  $
'      $Date:   27 Mar 2000 09:43:54  $
'     $Owner:   RN $
'   $Logfile:   Y:\cam350\rel\drl_lgnd.scv  $
'
'  Copyright (c) 1998-2001 Innoveda, Inc., all rights reserved.
'

' Tool Chart Script
' Written: Gordon Widera -- 6/13/94
' Modified: Jeff Miller -- 7/13/95
' Version: 2.0
' Modified: Ashley Morris -- 5/4/98
' Modified: Laszlo Jakusovszky -- 11/6/98, 7/8/99, 8/25/99, 1/27/00, 2/25/00
' Modified: Bruce Fihe -- 5/2/00, 5/9/00
'
'Updated 5/9/00:
'1. Warn user Units may have been changed, allow user to reset Units prior to exit.
'
'Updated 5/2/00:
'1. Forced CAM Editor
'2. Added user selection of Mils or MM

'Updated 2/23/00:
'1. Fixed bug with drilllegend! not returning dcodes every time. Needed to explicitly set active
'	tool table before using query
'2. Added message to warn user drill tools are being checked (for large tables or databases)
'3. Modified code to center database when zooming out to show user areas he can position table.

'Updated 1/27/00:
'1. Added code to use the new revamped drilllegend! query dcode for representing the drill hits in the table

'Updated 8/25/99:
'1. Updated to new usage of NC tool queries for drill tools
'	- uses arrays to track drill toll number, size and count
'	- due to elimination of "legend" dcodes in 6.0, created an array of dcodes corresponding
'	  to size of drill tools
'2. Sorts drill tools to make sure they're in tool # order, and not tool ref. order

'Updated 7/8/99:
'1. Added Form for user input

'Updated 11/6/98:
'1. Eliminated as many goto statements as possible
'2. Created code to allow user to set the text height instead of arbitrarily 
'   assigning size of 75; consequently use this height as the basis for 
'   generating tool chart formatting.
'   Appropriate warning is issued if text may be too small.
'3. Changed wording on placement prompt for mouse coordinates.
'4. Added ability to change location for chart, by erasing it and placing a
'   new one in another location
'5. Zoom out from center of pcb to give user a view as to where to place tool chart

' If there is no drill layer, quit the program.
10
If drilllayer! < 0 then goto 999
'
' Force CAM Editor because of reported problems running this macro in the NC Editor.
'
util_camed@
'
'Set default text height and scale
'
Textxscale@ 100
Height# = 75

25
OpenForm #1,60,6,"DRILL LEGEND MACRO by Advanced CAM Technologies", character, okcancel
'	Initial Info
AddForm #1, 0, 0, "This macro places a drill legend table on your database, in any position you choose",Print
AddForm #1, 0, 1, "",Print
AddForm #1, 1, 2, "Units", Choice, CurrentUnits, "Mils", "MM"
AddForm #1, 0, 5, "Please enter text height:",input,Height#
DisplayForm #1
Canceled% = formcancel(1)
DeleteForm #1

if Canceled% = 1 then goto 550

setunit@ (CurrentUnits -1)

if CurrentUnits = 1 then
    SizeFactor# = 1
else
    SizeFactor# = 39.37008
end if

'
' Define text.
'

Textangle@ 0
Textcspace@ 20.0000/SizeFactor#
Textlspace@ 100.0000
Textmirror@ 0
Textfitting@ 0
'Set text to center, will be used to center on given coordinates
Textjust@ 4


30
'Set text scale as necessary for smaller text, to retain legibility
If Height# < 75/SizeFactor# then
	OK_Cancel " WARNING: Table may be too small to read!"+CR!+"OK to continue? (Cancel to change text size)", OK
	If OK = 0 then goto 25
	Textxscale@ 75
	Textfullheight@ Height#
	If Height# < 25/SizeFactor# then Textxscale@ 50
Else
	Textfullheight@ Height#
End if
'
' Declare variables
'
40
line1 = 0
bar = 0
layer = 0
tool = 0
banx = 0
bany = 0

'
' Save the database extent.
'
50
minx = dbminx!
maxx = dbmaxx!
miny = dbminy!
maxy = dbmaxy!
ctrx = (maxx - (- minx)) / 2
ctry = (maxy - (- miny)) / 2

'
' Count up the number of drill tools.
'
dim ToolNum% (1 to 100)
dim ToolRef% (1 to 100)
dim ToolSize# (1 to 100)
dim ToolCount% (1 to 100)

setlayer@ drilllayer!
Table% = tooltable!		'finds active tool table #

print_msg "Getting Drill Tool Information"

For A = 1 to 100
	set_drill_tool@ A
	if toolused! = 1 then
		Counter% = Counter% + 1
		ToolNum%(Counter%) = toolnum!
		ToolRef%(Counter%) = A
		ToolSize#(Counter%) = toolsize!
		ToolCount%(Counter%) = lyrdrillcount!
	end if
next

For Lay% = 0 to highestlayer!
	setlayer@ Lay%
	if layertype! = 21 then
		if Lay% <> drilllayer! then
			for C = 1 to Counter%
				set_drill_tool@ ToolNum%(C)
				ToolCount%(C) = ToolCount%(C) + lyrdrillcount!
			next
		end if
	end if
next

'Sort drill tools small to large:
60
For T = 1 to Counter%
	Z = T + 1
	For X = Z to Counter%
		'	Check #T tool in last against all others
		'		- transcode info for smaller tool to become tool T
		if ToolNum%(T) > ToolNum%(X) then
			'	Swap smaller tools for larger ones
			swap ToolNum%(T), ToolNum%(X)
			swap ToolRef%(T), ToolRef%(X)
			swap ToolSize#(T), ToolSize#(X)
			swap ToolCount%(T), ToolCount%(X)
		end if
	next
next

close_msg


'
' Set the active layer for the Tool Chart.
'
100
Setlayer@ blanklayer!
Layernum = blanklayer!
edit_layer@ Layernum,-2,-2,-2,-2,"Chart.gbr"
update_layerbar@

'Zoom out to allow user to see where to place chart
view_all@
view_zoomout@
axy@ ctrx,ctry
end@

'
' Let user place tool chart.
'
print "Please click the mouse where you wish to place"+CR!+"the UPPER LEFT corner of the tool chart"
getmousepos@
banx = mouseposx!
bany = mouseposy! - (Height# * 3)
'
' Create a dcode to draw with.
'
tempdcode = blankdcode!
Edit_aperture@ tempdcode,1,5/SizeFactor#,5/SizeFactor#,tempdcode,""
'
' Draw tool chart header.
'
200
Setdcode@ tempdcode
' Base line-widths and heights on the given height
' of the text. (36 width unit used to keep chart even).
Wide = 36 * Height#
Tall = 3 * Height#
Tall2 = 2 * Height#
Tall1 = Height#
Add_line@
axy@ (banx),(bany + Tall)
axy@ (banx + Wide),(bany + Tall)
Add_text@
Addtext@ (banx + (Wide/2)),(bany + Tall2),0,0,"TOOL CHART"
Add_line@
axy@ (banx),(bany + Tall1)
axy@ (banx + Wide),(bany + Tall1)
Back@
Add_text@
Addtext@ (banx + (Wide/16)),(bany),0,0,"No."

If CurrentUnits = 1 then
    Size$ = "Size(mils)"
else
    Size$ = "Size(MM)"
end if

Addtext@ (banx + (Wide/3)),(bany),0,0, Size$
Addtext@ (banx + (Wide * .625)),(bany),0,0,"Count"
Addtext@ (banx + (Wide * .875)),(bany),0,0,"Legend"
Back@

' 
' Draw tool information.
'
' Place text and flashes centered on given coordinates.
line1 = 0

For N = 1 to Counter%

	'	Get tool information:
  	line1 = line1 + 1     
  	x$ = ToolNum%(N) 
	y# = ToolSize#(N)
    if CurrentUnits = 1 then
	    y$ = FMTUSING$("####.#", y#)
    else
	    y$ = FMTUSING$("#.####", y#)
    end if        
	z$ = ToolCount%(N)

	'	Add tool information to the chart:
	textjust@ 4
	Add_text@
	Addtext@ (banx + (Wide/16)),(bany - (line1 * Tall2)),0,0,x$
	Textjust@ 6
	Addtext@ (banx + (Wide * .375)),(bany - (line1 * Tall2)),0,0,y$
	Addtext@ (banx + (Wide * .671875)),(bany - (line1 * Tall2)),0,0,z$
	Add_line@
  	axy@ (banx),(bany - ((line1 * Tall2) - Tall1))
  	axy@ (banx + Wide),(bany - ((line1 * Tall2) - Tall1))


	'	Add Drill legend flash to chart:

	'		Set tool table
	set_nc_table@ Table%

	'		Retrieve tool information
	set_nc_toolref@ ToolRef%(N)
	ToolD% = dbq_drilllegend!
	DrillSize# = ToolSize#(N)

	'		Add flashes
	If DrillSize# < (Height#*2) then
		setdcode@ ToolD%
		add_flash@
  		axy@ (banx+ (Wide * .875)),(bany - (line1 * Tall2))
	else
		Dname$ = "D" + str$(ToolD%)
		print "Drill "ToolNum%(N)" is too large to draw in the table."
		Setdcode@ tempdcode
		Textjust@ 4
		Add_text@
		Addtext@ (banx+ (Wide * .875)),(bany - (line1 * Tall2)),0,0,Dname$
	end if
	setdcode@ tempdcode
next

Setdcode@ tempdcode
Back@

'
' Draw last lines and show all of pcb
'
' Generate lines using fractions of the overall length of the
' text to create even table formatting.
400
Add_line@
axy@ (banx),(bany - ((line1 * Tall2) + Tall1))
axy@ (banx + Wide),(bany - ((line1 * Tall2) + Tall1))
Add_line@
axy@ (banx),(bany + Tall)
axy@ (banx),(bany - ((line1 * Tall2) + Tall1))
Add_line@
axy@ (banx + (Wide/8)),(bany + Tall1)
axy@ (banx + (Wide/8)),(bany - ((line1 * Tall2) + Tall1))
Add_line@
axy@ (banx + (Wide/2)),(bany + Tall1)
axy@ (banx + (Wide/2)),(bany - ((line1 * Tall2) + Tall1))
Add_line@
axy@ (banx + (Wide * .75)),(bany + Tall1)
axy@ (banx + (Wide * .75)),(bany - ((line1 * Tall2) + Tall1))
Add_line@
axy@ (banx + Wide),(bany + Tall)
axy@ (banx + Wide),(bany - ((line1 * Tall2)+ Tall1))
Find_dcode@
setdcode@ tempdcode
update_dcodebar@
view_redraw@
View_all@
back@

500
'Allow user to change placement of drill legend chart
OK_cancel "Is this the correct position for your drill legend?", OK
If OK = 0 then
	edit_removelyr@ Layernum
	view_redraw@
	OK_cancel "Create a new drill table?", OK
	If OK = 1 then
		textjust@ 4
		goto 100
	end if
end if

OpenForm #2,50,5,"Drill Legend Macro - CAUTION", character, okcancel
'	Initial Info
AddForm #2, 0, 0, "This macro may have changed your Units setting.",Print
AddForm #2, 0, 1, "       Set Units as desired, below.",Print
AddForm #2, 1, 2, "Units", Choice, CurrentUnits, "Mils", "MM"
DisplayForm #2
Canceled% = formcancel(2)
DeleteForm #2

if Canceled% = 1 then
	goto 550
else
	setunit@ (CurrentUnits -1)
end if	

550
end
'
' If there is no drill data, give a warning and end here.
'
999
print "No drill layer present! Macro Exits."
end
